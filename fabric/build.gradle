plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath.set(project(":common").loom.accessWidenerPath)
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'Owo'
        url 'https://maven.wispforest.io/releases/'
    }

    maven {
        name = 'GeckoLib'
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
    }

    maven { // for athena/CSL
        name = 'teamresourceful'
        url "https://maven.teamresourceful.com/repository/maven-public/"
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    modRuntimeOnly "dev.emi:emi-fabric:1.1.22+1.21.1";

    modImplementation "curse.maven:ftb-filter-system-943925:6259287"
    modImplementation "curse.maven:ftb-library-fabric-438495:6711325"

    modRuntimeOnly "curse.maven:oritech-1030830:6807349"
    modRuntimeOnly "teamreborn:energy:4.1.0"
    modRuntimeOnly "io.wispforest:owo-lib:0.12.15.4+1.21"
    modRuntimeOnly "software.bernie.geckolib:geckolib-fabric-1.21.1:4.6.6"
    modRuntimeOnly "earth.terrarium.athena:athena-fabric-1.21:4.0.0"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

publishMods {
    file = remapJar.archiveFile
    changelog = file("../CHANGELOG.md").getText()
    type = BETA
    modLoaders.add("fabric")

    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_API_KEY")
        projectId = "kNthsBTM"
        minecraftVersions.add("1.21.1")

        requires("architectury-api")

        announcementTitle = "Fabric Version on Modrinth"
    }

    curseforge {
        accessToken = providers.environmentVariable("CURSEFORGE_API_KEY")
        projectId = "1292980"
        projectSlug = "simple-conveyor-belts"
        minecraftVersions.add("1.21.1")

        requires("architectury-api")

        javaVersions.add(JavaVersion.VERSION_21)
        clientRequired = true
        serverRequired = true

        announcementTitle = "Fabric Version on CurseForge"
    }
}
